#!/usr/bin/env perl
# Emacs: -*- mode: cperl; -*-
# For license: see LICENSE file at top-level

use strict;
use warnings;
use diagnostics;

use File::Basename;
my $progname = basename($0);

#
# -- encapsulate debugging output ----------------------------------------
#

use constant DEBUG => 1;

sub debug {
  if (DEBUG) {
    print STDERR "DEBUG($progname): @_\n";
  }
}

#
# -- work out which launcher is required ---------------------------------
#

my $launcher = '';

#
# anything in environment?
#
if (defined $ENV{SHMEM_LAUNCHER}) {
  $launcher = $ENV{SHMEM_LAUNCHER};
}

#
# configure might have given us a SHMEM_LAUNCHER
#
if ($launcher eq '') {
  $launcher = '@SHMEM_LAUNCHER@';
}

#
# still nothing, use default
#
if ($launcher eq '') {
  $launcher = 'prun';
}

#
# -- do the underlying launch of app -------------------------------------
#

sub bare_launch {
  return system $launcher, @ARGV;
}

#
# -- for PRRTE, control of prte daemon -----------------------------------
#

#
# controlling launched daemon
#
my ($prrte_pid, $prrte_pipe);

sub prrte_shutdown {
  #
  # shut down daemons
  #
  kill TERM => $prrte_pid;
  waitpid($prrte_pid,0);
  close $prrte_pipe;

  debug("PRRTE shut down (pid $prrte_pid)...");
}

sub prrte_launch {
  #
  # start daemon in child process
  #
  $prrte_pid = open $prrte_pipe, '-|', 'prte';

  if (! defined $prrte_pid) {
    die "$progname: failed to run PRRTE daemon: $!\n";
  }

  debug("PRRTE wait (pid $prrte_pid)...");

  #
  # parent waits for daemon to be ready, then launches user app
  #
  if ($prrte_pid) {
    my $ready = 0;

    while (<$prrte_pipe>) {
      if (/^DVM ready/) {
	$ready = 1;
	last;
      }
    }

    if (! $ready) {
      prrte_shutdown();

      die "$progname: internal error at line " .
	__LINE__ .
	", should not be here\n";
    }

    debug("PRRTE ready (pid $prrte_pid)...");

    #
    # assume worst; if app is good this will later be 0
    #
    my $status = 1;

    if ($ready) {
      $status = bare_launch();
    }

    return $status;
  }
}

#
# -- let's do it! --------------------------------------------------------
#

my $err;

#
# if we're using PRRTE, fire up support, otherwise just launch
#

my $is_prun = basename($launcher) eq 'prun';

if ($is_prun) {
  $err = prrte_launch();

  prrte_shutdown();
}
else {
  $err = bare_launch();
}

exit $err;
